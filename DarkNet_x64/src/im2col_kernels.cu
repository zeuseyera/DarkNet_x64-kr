#include "cuda_runtime.h"
#include "curand.h"
#include "cublas_v2.h"

extern "C" {
#include "im2col.h"
#include "cuda.h"
}

// src: https://github.com/BVLC/caffe/blob/master/src/caffe/util/im2col.cu
// You may also want to read: https://github.com/BVLC/caffe/blob/master/LICENSE

__global__ void im2col_gpu_kernel( const int n
								, const float* data_im
								, const int height		// 사비 세로크기
								, const int width		// 사비 가로크기
								, const int ksize		// 포집 가로,세로
								, const int pad			// 패딩
								, const int stride		// 포집 보
								, const int height_col	// 출력 세로크기
								, const int width_col	// 출력 가로크기
								, float *data_col )
{
    int index = blockIdx.x*blockDim.x+threadIdx.x;

    for ( ; index < n; index += blockDim.x*gridDim.x )
	{
        int w_out		= index % width_col;
        int h_index		= index / width_col;
        int h_out		= h_index % height_col;
        int channel_in	= h_index / height_col;
        int channel_out	= channel_in * ksize * ksize;
        int h_in		= h_out * stride - pad;
        int w_in		= w_out * stride - pad;

        float* data_col_ptr = data_col;

        data_col_ptr += (channel_out * height_col + h_out) * width_col + w_out;

        const float* data_im_ptr = data_im;
        data_im_ptr += (channel_in * height + h_in) * width + w_in;

        for ( int i=0; i<ksize; ++i )
		{
            for ( int j=0; j<ksize; ++j )
			{
                int h = h_in + i;
                int w = w_in + j;

                *data_col_ptr = ( h >= 0 && w >= 0 && h < height && w < width ) ?
                    data_im_ptr[i * width + j] : 0;

                //*data_col_ptr = data_im_ptr[ii * width + jj];

                data_col_ptr += height_col * width_col;
            }
        }
    }
}

void im2col_ongpu( float *im	// 사비갑 시작주소()
				, int channels	// 사비 판수
				, int height	// 사비 세로
				, int width		// 사비 가로
				, int ksize		// 포집 가로,세로
				, int stride	// 포집 보
				, int pad		// 패딩
				, float *data_col )
{
    // We are going to launch channels * height_col * width_col kernels, each
    // kernel responsible for copying a single-channel grid.
	// channels * height_col * width_col 의 쿠다커널을 실생한다,
	// 단일-채널 격자 복사를 위해 책임지는 쿠다커널.
    int height_col = (height + 2 * pad - ksize) / stride + 1;
    int width_col = (width + 2 * pad - ksize) / stride + 1;
    int num_kernels = channels * height_col * width_col;

    im2col_gpu_kernel<<<(num_kernels+BLOCK-1)/BLOCK, BLOCK>>>( num_kernels
															, im
															, height
															, width
															, ksize
															, pad
															, stride
															, height_col	// 출력 세로크기
															, width_col		// 출력 가로크기
															, data_col );
}
